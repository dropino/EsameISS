System -msglog tearoomsys 
//mqttBroker "localhost" : 1883 eventTopic "unibo/waiter"   
  
//MESSAGES
mqttBroker "localhost" : 1883 eventTopic "unibo/basicrobot"   

//waiterwalker-walker 
Event    waiterwalkerstarted   : waiterwalkerstarted(X)
Event    walkerstarted   	: walkerstarted(X)

Request	 doPlan 			: doPlan(X,Y)
Reply 	 walkerError 		: walkerError( $XT,$YT )
Reply	 walkerDone 		: walkerDone(X,Y)

//Waiter-Waiterwalker
Event    waiterwalkerstarted : waiterwalkerstarted(X)

Request  moveForTask 		: moveForTask(TASK, N)
Reply    movementDone   	: movementDone(OK)
Reply	 walkbreak  		: walkbreak(X,Y)

//Waiter-itself
Dispatch rest				: rest(V)
Dispatch listen				: listen(V)

//ENTERING THE TEAROOM
//client - smartbell
Request  ringBell 			: ringBell(TEMP)
Reply 	 tempStatus 		: tempStatus(STATUS, CID) 

//smartbell - waiter
Dispatch clientID	 		: clientID (CLIENTID)

//waiter -> client
Request	deploy				: deploy(FROM, TO, CID)
Reply	arrived				: arrived(V) //V is either the table number or the exitdoor

Request waitTime			: waitTime(CID)
Reply	wait				: wait(TIME)

Event 	 deliver 			: deliver (TEA, TABLE) 
 
//ORDERING
//client -> waiter
Request  clientRequest		: clientRequest(KIND, TABLE, CID)
Reply	 atTable			: atTable(V) //depending on KIND, V contains "ok" for ordering and the amount owed if "pay"

Dispatch order				: order(TEA)
Dispatch pay				: pay(MONEY)

//waiter-barman
Dispatch sendOrder 			: sendOrder(TEA, TABLE, CID) 
Dispatch orderReady			: orderReady(TEA, TABLE, CID)

//TABLE DIRTY NOTIFICATION
Dispatch tableDirty 		: tableDirty(N)

Context ctxtearoom 		ip [host="localhost" 	port=8072] 
Context ctxsmartbell	ip [host="localhost" 	port=8071] 
Context ctxbarman 		ip [host="localhost" 	port=8070] 
Context ctxwalker 		ip [host="localhost" 	port=8050] 

CodedQActor walker context ctxwalker className "it.unibo.walker.Walker"  

QActor waiter context ctxtearoom {
	
	[#
		var CCID 			= ""	
		var CTABLE : Int?	= 0
		var BILL			= 5
		var KIND			= ""
		var CDRINK			= ""
		var WaitingClient : Boolean	= false
		var MaxStayTime		= 20000L
		var MaxWaitTime		= 30000L
		var TimeToGoHome	= 15000L
		
		var Ntables			= 0
		val clientQueue : Queue<String> = LinkedList<String>()
		
		var CurST			= ""
		var PL				= ""
		var Dest			= ""

		var wJson = json.WaiterJson()
	#]
	
	State s0 initial {	
		println("WAITER | INIT ")
 		discardMsg Off
		
 		solve( consult("tearoomkb.pl")	 )			
 		println("WAITER | solved tearoomkb.pl ")
 		
 		solve(  maxStayTime(T)  )
 		ifSolved {
 			[# MaxStayTime = getCurSol("T").toString().toLong() #]
 		}
 		solve(  maxWaitTime(T)  )
 		ifSolved {
 			[# MaxWaitTime = getCurSol("T").toString().toLong() #]
 		}
 		solve(  timeToGoHome(T)  )
 		ifSolved {
 			[# TimeToGoHome = getCurSol("T").toString().toLong() #]
 		}
 		println("Current MaxStay: $MaxStayTime, MaxWait: $MaxWaitTime, TimeToGoHome: $TimeToGoHome")

 	}	
  	Transition t0 whenEvent waiterwalkerstarted -> listening
  	
  	 
  	State listening {
  		[#
			CCID = ""
			CTABLE = 0

			wJson.reset()
  		#]
  		println("WAITER | listening... ")
 		updateResource [#wJson.toJson()#]
	}
	Transition t0  	whenTimeVar	TimeToGoHome		-> goHome
	  				whenRequest waitTime			-> answerTime
	  				whenRequest	deploy			 	-> handleDeploy
					whenRequest	clientRequest 		-> answerClientRequest
					whenMsg 	orderReady 	 		-> getDrink
					whenMsg 	tableDirty	 		-> cleanTable
						
  	
  	State goHome {
  		solve( waiter(ST) ) 
		ifSolved { 
			[# CurST = getCurSol("ST").toString()  #] 
			println("WAITER | current waiter state: $CurST")
		}
		println("WAITER | current waiter state: $CurST")
		
  		
  		//se non sono a casa ci vado
  		if [# CurST != "athome" #] {  			
 			request waiterwalker -m moveForTask : moveForTask(home, 1)
 			println("WAITER | Changing movement state")
 			
 			solve ( changeWaiterState(athome) )	
 			ifSolved {
 				 			[#
 				wJson.setMovingTo("home")
 			#]
 			}
 			updateResource [#wJson.toJson()#] 
  		} 
		else{ 
			forward waiter -m rest : rest(0)
		}		
  	}
	  Transition t0 	whenReply	movementDone 	-> listening
	  	  				whenReply	walkbreak	 	-> error
	  					whenMsg		rest	 		-> listening
  		
  	
  	State answerTime {
  		[# var WaitTime = 0L #]
  		
  		solve( numavailabletables(N) ) 
		ifSolved { 
			[# Ntables = getCurSol("N").toString().toInt()  #] 
			println("WAITER | numavailabletables=$Ntables")
		}
		
		onMsg(waitTime : waitTime(CID)) {
			[#CCID = payloadArg(0).toString()#]
		}
		
		if [# Ntables != 0 #] {
			
			[# 
				if (WaitingClient) {
					WaitingClient = false
					wJson.setAcceptedWaiting(true)
				} 
			#]
			
			replyTo waitTime with wait : wait(0)
		} else {
			[# 
				WaitTime = MaxWaitTime
				WaitingClient = true
				clientQueue.add(CCID) 
			#]
			replyTo waitTime with wait : wait($WaitTime)
		}
		[#
			wJson.setBusy(true)
			wJson.setWaitTime(WaitTime)
			wJson.setClientID(CCID)
			wJson.setReceivedRequest("waitTime")
		#]
		updateResource [#wJson.toJson()#]
  	}
  	Goto listening

  	
  	State handleDeploy {
  		println("WAITER | handling deployment request... ")  		
  		onMsg(deploy : deploy(FROM, TO, CID)) {
  			[#
  				CTABLE = payloadArg(0).toString().toIntOrNull()
  				Dest = payloadArg(1).toString()
  				CCID = payloadArg(2).toString()	
  			#]
  		}		
  	}
  	Goto goToEntrance if [# Dest == "table" #] else deployClientExit


	State goToEntrance {
		
		[#		
			wJson.setBusy(true)
			wJson.setClientID(CCID)
			wJson.setMovingTo("entrancedoor")
			wJson.setReceivedRequest("DeployEntrance")	  				
		#]
		
		 updateResource [#wJson.toJson()#] 

		println("WAITER | GOING to ENTRANCE door ")
		request waiterwalker -m moveForTask : moveForTask(entrancedoor, 1)
		solve ( changeWaiterState(moving) )		
	} 
	 Transition t0		whenReply	movementDone -> deployClientEntrance
	  					whenReply	walkbreak	 -> error
	
	
	State deployClientEntrance {		
		solve( tableavailable(N) ) 
		ifSolved { [# CTABLE = getCurSol("N").toString().toInt()  #] 
			println("WAITER | tableavailable=$CTABLE")					 
			solve( engageTable($CTABLE, $CCID ) ) 
			println("WAITER | DEPLOYING simclient $CCID to table $CTABLE")	
	 	}
	 	
	 	[#		
			wJson.setBusy(true)
			wJson.setClientID(CCID)
			wJson.setMovingFrom("entrancedoor")
			wJson.setMovingTo("table " + CTABLE)
			wJson.setTable(CTABLE)
			wJson.setReceivedRequest("deployEntrance")	  				
		#]
		
		 updateResource [#wJson.toJson()#] 
	 	
		request waiterwalker -m moveForTask : moveForTask(teatable, $CTABLE)
	} 	
	 Transition t0		whenReply	movementDone -> confirmClientArrival
	  					whenReply	walkbreak	 -> error	


	State deployClientExit {
		
		println("WAITER | deploying simclient from table to exit door... ")
		forward waiter -m tableDirty : tableDirty($CTABLE)

		request waiterwalker -m moveForTask : moveForTask(exitdoor, 1)
		
		[#		
			wJson.setBusy(true)
			wJson.setClientID(CCID)
			wJson.setTable(CTABLE)
			wJson.setTableDirty(true)
			wJson.setMovingFrom("table " + CTABLE)
			wJson.setMovingTo("exitdoor")
			wJson.setReceivedRequest("deployExit")	  				
		#]
		
		 updateResource [#wJson.toJson()#] 
		
	} 	
	 Transition t0		whenReply	movementDone -> confirmClientArrival
	  					whenReply	walkbreak	 -> error	
	  	
	  					
	State confirmClientArrival {		
		println("WAITER | SENDING CONFIRMATION to simclient $CCID: arrived $Dest $CTABLE")
		
		replyTo deploy with  arrived	: arrived($CTABLE)
		
		[#		
			wJson.setMovingFrom("")
			wJson.setMovingTo("")
			wJson.setArrival(Dest)
		#]
		 updateResource [#wJson.toJson()#] 
	} 
	Goto listening
		
						 
	State answerClientRequest {
		onMsg(clientRequest : clientRequest(KIND, TABLE, CID)) {
			[#  
				KIND	= payloadArg(0).toString()
				CTABLE 	= payloadArg(1).toString().toInt()
				CCID 	= payloadArg(2).toString()
				wJson.setBusy(true)
				wJson.setMovingTo("table " + CTABLE)
				wJson.setReceivedRequest(KIND)
				wJson.setClientID(CCID)
				wJson.setTable(CTABLE)
			#]
		}
		
		updateResource [#wJson.toJson()#]
		
		println("WAITER | received CLIENTREQUEST $KIND $CTABLE ")
		
		request waiterwalker -m moveForTask : moveForTask(teatable, $CTABLE)
		solve ( changeWaiterState(moving) )	
	}
	 Transition t0		whenReply	movementDone -> atTableForRequest
	  					whenReply	walkbreak	 -> error	
	
	
	State atTableForRequest {
		solve( clientRequestPayload($KIND, P) )
		ifSolved {
			[# PL = getCurSol("P").toString() #]
		}
			println("WAITER | CLIENT $CCID requested $KIND")
		
		[#				
			wJson.setMovingTo("")
			wJson.setArrival("table " + CTABLE)
		#]
		updateResource [#wJson.toJson()#]
		
		replyTo clientRequest with atTable : atTable($PL)
	}
	Transition t0 		whenMsg 	order 		-> transferOrder
						whenMsg 	pay 		-> payment 
	
	State payment {
		onMsg(pay : pay(MONEY)) {
			[#  
				var amount	= payloadArg(0).toString()
				wJson.reset()
				wJson.setBusy(true)
				wJson.setPayment(amount.toInt())
				wJson.setReceivedRequest(KIND)
				wJson.setClientID(CCID)
				wJson.setTable(CTABLE)
			#]
			updateResource [#wJson.toJson()#]
		}

	}
	Transition t0 whenRequest deploy -> handleDeploy
	
	State transferOrder {
		println("WAITER | sending order to barman $CCID $CDRINK $CTABLE... ")
		onMsg(order : order(DRINK)) {
			[#  CDRINK	= payloadArg(0).toString() #]
		}
		
		[#	
				wJson.reset()
				wJson.setBusy(true)
				wJson.setOrder(CDRINK)
				wJson.setReceivedRequest(KIND)
				wJson.setClientID(CCID)
				wJson.setTable(CTABLE)
		#]
		updateResource [#wJson.toJson()#]
		forward barman -m sendOrder : sendOrder($CDRINK, $CTABLE, $CCID)
	}
	Goto listening
	
	
	State cleanTable {
		onMsg(tableDirty : tableDirty(N)) {
			[#  CTABLE = payloadArg(0).toString().toInt() #]
		}
		println("WAITER | going to table for CLEANING $CTABLE... ")
		request waiterwalker -m moveForTask : moveForTask(teatable, $CTABLE)
		solve ( changeWaiterState(moving) )	
		[#
			wJson.setBusy(true)
			wJson.setTable(CTABLE)
			wJson.setTableDirty(true)
			wJson.setMovingTo("table "+ CTABLE)
			wJson.setReceivedRequest("tableDirty")						
		#]
	}
	Transition t0		whenReply	movementDone -> atTableToClean
	  					whenReply	walkbreak	 -> error
	
	
	State atTableToClean {	
		println("WAITER | cleaning table $CTABLE... ")
		[#
			wJson.setBusy(true)
			wJson.setTable(CTABLE)
			wJson.setMovingTo("")
			wJson.setTableDirty(true)
			wJson.setArrival("table " + CTABLE)
			wJson.setReceivedRequest("tableDirty")						
		#]
		updateResource [# wJson.toJson() #]
		delay 5000
		[#
			wJson.setArrival("")
			wJson.setTableDirty(false)
		//We use to return null if there are no more clients waiting
			wJson.setClientID(clientQueue.poll())
		#]
		updateResource [# wJson.toJson() #]
		solve( cleanTable($CTABLE) )
	}
	Goto listening

	
	State getDrink {
		onMsg(orderReady : orderReady(TEA, TABLE, CID)) {
			[# 
				CDRINK = payloadArg(0).toString()
				CTABLE = payloadArg(1).toString().toInt()
				CCID =  payloadArg(2).toString()
			#]
		}
		println("WAITER | going to barman... ")
		request waiterwalker -m moveForTask : moveForTask(barman, 1)
		solve ( changeWaiterState(moving) )
		
		[#
			wJson.setBusy(true)
			wJson.setTable(CTABLE)
			wJson.setReceivedRequest("drinkReady")
			wJson.setOrder(CDRINK)
			wJson.setMovingTo("barman")	
			wJson.setClientID(CCID)					
		#]
		updateResource [# wJson.toJson() #]
	}
	Transition t0 		whenReply	movementDone -> bringDrinkToClient
	  					whenReply	walkbreak	 -> error	
	
	
	State bringDrinkToClient {
		println("WAITER | taking $CDRINK... ")
		println("WAITER | GOING TO CLIENT $CCID table $CTABLE... ")
		[#
			wJson.setMovingFrom("barman")	
			wJson.setMovingTo("table " + CTABLE)					
		#]
		updateResource [# wJson.toJson() #]

		request waiterwalker -m moveForTask : moveForTask(teatable, $CTABLE)
	}
	Transition t0 		whenReply	movementDone -> leaveDrinkAtTable
	  					whenReply	walkbreak	 -> error	
	
			
	State leaveDrinkAtTable {
		println("WAITER | GIVING the CLIENT the tea")
		[#
			wJson.setMovingFrom("")	
			wJson.setMovingTo("")
			wJson.setArrival("table " + CTABLE)					
			wJson.setWaitTime(MaxStayTime)
		#]
		updateResource [# wJson.toJson() #]
		emit deliver : deliver(tea, $CTABLE)
	}
	Goto listening
	
	
	State error {
		[#
			wJson.reset()
			wJson.setArrival("error")
		#]
		updateResource [# wJson.toJson() #]
		println("&&& WAITER | an error occurred while walking. ")
	} Goto listening
}





QActor waiterwalker context ctxtearoom {  
	[#
		var XT = "0"
		var YT = "0"
		var TASK	= "" 
		var N		= ""
	#] 


	State s0 initial {	     
 		println("waiterwalker | INIT")
 		solve( consult("waiterwalkerkb.pl")	 )
		println("waiterwalker | STARTS")
		emit waiterwalkerstarted : waiterwalkerstarted(ok)
	}	
	Transition t0 whenEvent walkerstarted -> waitCmd   

	 
	State waitCmd {	     
 		println("waiterwalker | waiting for a moveForTask message")
	}	
	Transition t0 whenRequest moveForTask -> locateObjective
 	
 	
	State locateObjective{
		printCurrentMessage
		onMsg( moveForTask : moveForTask(TASK, N) ){
			[# 
				TASK = payloadArg(0).toString()
				N = payloadArg(1).toString()
			#]
			println("waiterwalker | task received: $TASK, jolly: $N") 
			solve( pos($TASK,$N,X,Y)  )  
			ifSolved { 
				[# 
					XT = getCurSol("X").toString()
					YT = getCurSol("Y").toString()
				#] 
 			}
			println("waiterwalker | sending objective location ($XT,$YT) to walker")
			request walker -m doPlan : doPlan($XT,$YT)		
		}
	}
	Transition t1 whenReply walkerDone -> movementCompleted
				  whenReply walkerError -> movementError


	State movementCompleted{
		println("waiterwalker | POINT ($XT,$YT) REACHED")
	    replyTo moveForTask with movementDone : movementDone(OK) 					
	}
	Goto waitCmd
	
	
	State movementError{
		println("waiterwalker | FAILS")
		printCurrentMessage
		onMsg( moveForTask : moveForTask(X, Y) ){
			[# 
				XT = payloadArg(0).toString()
				YT = payloadArg(1).toString()
			#]
	  		replyTo moveForTask with walkbreak : walkbreak( $XT, $YT ) 	
		}
	}
	Goto waitCmd  
 }
 
 
 


QActor smartbell context ctxsmartbell {
 [# 
 	var Temp = 0
 	var CID = 0 
 	val sJson = json.SmartBellJson()
 	
 	var id = 0
 #]
 
 
	State s0 initial {
		discardMsg Off
		println("SmartBell | INIT")
	}
	Goto waitForClient
	
	
	State waitForClient {
		println("  SmartBell | Wait Client  ")
		[#
			sJson.reset()
		#]
		updateResource[#sJson.toJson()#]
	
	}
	Transition t0 whenRequest ringBell -> checkTemp
	 
	 
	State checkTemp {
		onMsg ( ringBell : ringBell (T) ) {
			[#
				Temp = (36..38).random()
				sJson.setBusy(true)
				sJson.setClientArrived(true)
				
			#]
			updateResource[#sJson.toJson()#]
			println("  SmartBell | Check Temp $Temp ")	 
		}
	}
	Goto handleEventTempOK if [# Temp < 37.5 && Temp >= 35.5 #] else handleEventTempKO
	
	
	State handleEventTempKO {
		println("  SmartBell | Temp is over  ")
		println("  SmartBell | Client Discard  ")
		replyTo ringBell with tempStatus : tempStatus (0, $CID)
		[#
			sJson.setBusy(true)
			sJson.setClientArrived(false)
			sJson.setClientDenied(true)
		#]
		updateResource[#sJson.toJson()#]				
	}
	Goto waitForClient
	
	
	State handleEventTempOK { 
		println("  SmartBell | Temp is OK  ")
		println("  SmartBell | Client Accepted  ")
		[# 	
			id++;
			CID = id; 
			sJson.setClientArrived(false)
			sJson.setClientAccepted(true)
		#]		
		updateResource[#sJson.toJson()#]		

		replyTo ringBell with tempStatus : tempStatus (1, $CID)
		forward waiter -m clientID : clientID ($CID)
	}
	Goto waitForClient		
}





QActor barman context ctxbarman {
	[#
		var CTABLE = 0
		var CTEA = ""
		var CCID = ""
		var bJson = json.BarmanJson()
	#]	
	
	State s0 initial {
		discardMsg Off  
		println("  Barman | INIT  ")
	}
	Goto waitForNewOrder


	State waitForNewOrder {
		println("  Barman | Waiting Order  ")
		[#
			CTABLE = 0
			bJson.setBusy(false)
			bJson.setPreparingForTable(-1)
			bJson.setPreparingOrder("")
			bJson.setOrderReadyTable(-1)
			bJson.setOrderReady(false)
		#]
		updateResource [#bJson.toJson()#]
	}
	Transition t0 whenMsg sendOrder -> prepare
	
	
	State prepare {
		onMsg( sendOrder : sendOrder(TEA, TABLE, CID) ){
			[#
				CTEA = payloadArg(0).toString()
				CTABLE = payloadArg(1).toString().toInt()
				CCID = payloadArg(2).toString()
				
				bJson.setBusy(true)
				bJson.setPreparingForTable(CTABLE)
				bJson.setPreparingOrder(CTEA)
			#]
			updateResource [#bJson.toJson()#]
			println("  Barman | Making $CTEA for client $CCID sit at table $CTABLE")
			
		}
		
		delay 5000
		forward waiter -m orderReady : orderReady($CTEA, $CTABLE, $CCID)
			println("  Barman | $CTEA READY to be served to client $CCID at table $CTABLE")
			[#
				bJson.setPreparingForTable(-1)
				bJson.setPreparingOrder("")
				bJson.setOrderReadyTable(CTABLE)
				bJson.setOrderReady(true)
			#]
			updateResource [#bJson.toJson()#]
	}
	Goto waitForNewOrder
}